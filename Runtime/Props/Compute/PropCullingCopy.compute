#pragma kernel CSPropCullingCopy
#define UNITY_INDIRECT_DRAW_ARGS IndirectDrawIndexedArgs
#include "UnityIndirect.cginc"
#include "Packages/com.jedjoud.voxelterraingenerator/Runtime/Utils/PropPackUtils.cginc"

RWStructuredBuffer<uint> culledCount;
StructuredBuffer<uint4> permProps;

RWStructuredBuffer<uint4> culledProps;
StructuredBuffer<uint> usedBitmask;

StructuredBuffer<uint3> propSectionOffsets;
StructuredBuffer<float> maxDistances;

float3 cameraForward;
float3 cameraPosition;

// Do some culling on the props and save them to the culled props buffer
[numthreads(32, 1, 1)]
void CSPropCullingCopy(uint3 id : SV_DispatchThreadID)
{
	uint permOffset = propSectionOffsets[id.y].y;
	uint visibleOffset = propSectionOffsets[id.y].z;
	float maxDistance = maxDistances[id.y];
	uint inputIndex = id.x + permOffset;

	float4 positionScale = UnpackPositionAndScale(permProps[inputIndex].xy);

	uint block = inputIndex / 32;
	uint local = inputIndex % 32;
	bool valid = ((usedBitmask[block] >> local) & 1) == 1;
	//valid = true;

	float3 diff = cameraPosition - positionScale.xyz;
	float mag = length(diff);
	diff = normalize(diff);

	bool visible = dot(diff, cameraForward) < -0.2 && mag < maxDistance;
	//visible = true;

	if (visible && valid) {
		int index;
		InterlockedAdd(culledCount[id.y], 1, index);
		culledProps[index + visibleOffset] = permProps[inputIndex];
	}
}