#pragma kernel CSCull

#include "Packages/com.jedjoud.voxelterrain/Runtime/Compute/Props.cginc"

StructuredBuffer<uint4> perm_buffer;
RWStructuredBuffer<uint> indirection_buffer;
StructuredBuffer<uint> perm_props_in_use_bitset_buffer;
StructuredBuffer<int> perm_buffer_counts_buffer;
StructuredBuffer<int> perm_buffer_offsets_buffer;

RWStructuredBuffer<uint> visible_props_counters_buffer;

int max_combined_perm_props;
int types;
float3 camera_position;
float3 camera_forward;

[numthreads(32, 1, 1)]
void CSCull(uint3 id : SV_DispatchThreadID) {
	int block = perm_props_in_use_bitset_buffer[id.x];

	if (block == 0) {
		return;
	}

	for (int i = 0; i < 32; i++) {
		int local = i;
		int index = id.x * 32 + i;

		int type = -1;
		for (int j = 0; j < types; j++) {
			if (index >= perm_buffer_offsets_buffer[j]) {
				type = j;
			} else {
				break;
			}
		}

		if (type == -1) {
			continue;
		}
		
		if (index >= max_combined_perm_props) {
			return;
		}

		bool valid = ((block >> local) & 1) == 1;
		bool visible = false;

		float4 position_scale;
		float4 rotation;

		uint4 prop = perm_buffer[index];
		position_scale = UnpackPositionAndScale(prop.xy);
		rotation = UnpackRotation(prop.zw);

		visible = dot(normalize(position_scale.xyz - camera_position), camera_forward) > 0.6;
		
		if (valid && visible) {
			uint dst_index;
			InterlockedAdd(visible_props_counters_buffer[type], 1u, dst_index);
			dst_index += (uint)perm_buffer_offsets_buffer[type];
			indirection_buffer[dst_index] = index;
		}
	}
}